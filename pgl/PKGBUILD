# Maintainer: Gilrain <gilrain+libre.arch A_T castelmo DOT_ re>

pkgname=pgl-git
pkgver=2.3.1.1.r77.g9d91ab6
pkgrel=1
pkgdesc='A privacy oriented firewall application (Daemon, CLI & GUI).'
arch=('i686' 'x86_64')
# Maintainer: Gilrain <gilrain+libre.arch A_T castelmo DOT_ re>
url='http://sourceforge.net/projects/peerguardian/'
license=('GPL3')
depends=('bash' 'eudev' 'dbus' 'coreutils' 'procps-ng' 'logrotate' 'wget' 'iptables' 'libnetfilter_queue' 'cron' 'qt5-base' 'whois')
optdepends=('unzip: for zipped blocklists'
        'p7zip: for 7z blocklists'
            'net-tools: to whitelist local IP addresses'
            'networkmanager: to whitelist newly started network interfaces'
            'smtp-forwarder: to send reports'
        'tcptraceroute: to diagnose connection problems')
makedepends=('git' 'zlib')
license=('GPL3')
provides=('pgl')
conflicts=('pgl' 'pgl-cli')
backup=('etc/pgl/allow.p2p'
    'etc/pgl/blocklists.list'
    'etc/pgl/pglcmd.conf'
    'etc/logrotate.d/pglcmd'
    'etc/logrotate.d/pgld')
source=("git://git.code.sf.net/p/peerguardian/code/"
        https://raw.githubusercontent.com/eserlxl/pgl-git-AUR-Patches/main/pgld-gcc10fix.patch)
sha256sums=('SKIP'
            'b4cf3cb48620e04c5115be1db73eb25fb9c779120e4930b3e5d4bd04d8c4a13c')

pkgver() {
    cd code

    git describe --tags | sed 's/^debian\///;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd $srcdir/code
    patch -Np1 -i ../pgld-gcc10fix.patch
    ./autogen.sh
}

build() {
    cd code

    ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --datarootdir=/usr/share \
    --with-piddir=/run \
    --without-systemd \
    --without-initddir
    make CFLAGS=-fno-common
}

package() {
    cd code

    make DESTDIR="$pkgdir" install
}
